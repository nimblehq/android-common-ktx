plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

apply from: '../config/jacoco.gradle'
apply from: '../config/detekt.gradle'

def versionPropertiesFile = rootProject.file("./version.properties")
def versionProperties = new Properties()
versionProperties.load(new FileInputStream(versionPropertiesFile))

android {
    compileSdk 31

    defaultConfig {
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation(
        "androidx.core:core-ktx:1.6.0",
        "androidx.appcompat:appcompat:1.3.1",
        "com.google.android.material:material:1.4.0",
        "com.google.code.gson:gson:2.8.9"
    )

    androidTestImplementation(
        "androidx.test.ext:junit:1.1.3",
        "androidx.test.espresso:espresso-core:3.4.0",
        "org.hamcrest:hamcrest-library:1.3"
    )

    testImplementation "junit:junit:4.13.2"
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = 'co.nimblehq'
                artifactId = 'extensions'
                version = versionProperties['versionName']
            }
        }
    }
}
